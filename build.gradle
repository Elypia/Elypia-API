/*
 * Copyright 2019-2020 Elypia CIC
 * Licensed under the Apache License, Version 2.0 (the "License");
 *
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *       http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 *
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java"
    id "application"
    id "idea"
    id "jacoco"
    id "org.springframework.boot" version "2.3.1.RELEASE"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
}

group = "org.elypia"
version = "1.0.0"
description = "The core API that most of Elypia's services are interfacing with."
sourceCompatibility = 11
targetCompatibility = 11

ext {
    elypiaiVersion = "4.2.0"
    springVersion = "2.3.1.RELEASE"
    hibernateVersion = "5.4.17.Final"
    validationVersion = "2.0.2"
}

repositories {
    mavenCentral()
    
    // Use Dependencies Locally
    mavenLocal()
}

dependencies {
    // Elypiai
    implementation "org.elypia.elypiai:companies-house:${elypiaiVersion}"

    // Spring
    implementation "org.springframework.boot:spring-boot-starter-web:${springVersion}"
    implementation "org.springframework.boot:spring-boot-starter-security:${springVersion}"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springVersion}"

    // Database
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springVersion}"
    implementation "org.hibernate:hibernate-core:${hibernateVersion}"
    runtimeOnly "org.hibernate:hibernate-c3p0:${hibernateVersion}"
    runtimeOnly "mysql:mysql-connector-java:8.0.20"

    // Validation
    implementation "jakarta.validation:jakarta.validation-api:${validationVersion}"
    implementation "org.hibernate.validator:hibernate-validator:6.1.5.Final"

    // Stripe
    implementation "com.stripe:stripe-java:19.24.0"

    // GitLab
    implementation "org.gitlab4j:gitlab4j-api:4.14.30"

    // Logging
    implementation "org.slf4j:slf4j-api:1.7.30"
    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"
    runtimeOnly "com.google.cloud:google-cloud-logging-logback:0.117.0-alpha"

    // Testing
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springVersion}"
    testImplementation "org.springframework.security:spring-security-test:5.3.3.RELEASE"
    testRuntimeOnly "com.h2database:h2:1.4.200"
}

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

bootJar {
    baseName = "elypia-api"
    version = this.version
}

/**
 * Testing and Code Coverage
 */

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    reports {
        html.enabled = false
    }
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = false
    }
}
